apply plugin: 'com.android.application'
apply plugin: 'net.saliman.cobertura'
apply plugin: 'com.github.kt3k.coveralls'

configurations {
    natives
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "es.danirod.rectball.android"
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 408
        versionName "0.4.8-dev"
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        debug {
            buildConfigField "boolean", "FINE_DEBUG", "true"
            buildConfigField "boolean", "ANALYTICS_DRY_RUN", "true"
        }
        release {
            buildConfigField "boolean", "FINE_DEBUG", "false"
            buildConfigField "boolean", "ANALYTICS_DRY_RUN", "false"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // libGDX
    compile "com.badlogicgames.gdx:gdx-backend-android:1.9.2"
    natives "com.badlogicgames.gdx:gdx-platform:1.9.2:natives-armeabi"
    natives "com.badlogicgames.gdx:gdx-platform:1.9.2:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-platform:1.9.2:natives-x86"

    // Testing libraries.
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-all:1.10.19"
    testCompile "com.badlogicgames.gdx:gdx-backend-headless:1.9.2"
    testCompile "com.badlogicgames.gdx:gdx:1.9.2"
    testCompile "com.badlogicgames.gdx:gdx-platform:1.9.2:natives-desktop"
    testCompile "org.slf4j:slf4j-api:1.7.10"

    // Android compatibility libraries.
    compile "com.android.support:appcompat-v7:25.3.1"

    // Google Play Services. DON'T BUMP to 10.2.0 or it will require a min SDK version of 14.
    // TODO: Investigate how many people are still playing on Android 2.3 and Android 3.0.
    compile "com.google.android.gms:play-services-analytics:10.0.1"
    compile "com.google.android.gms:play-services-games:10.0.1"
}

cobertura.coverageFormats = ['html', 'xml']

// Extract the libGDX natives from the JARs.
task copyAndroidNatives() {
    file("src/main/jniLibs/armeabi/").mkdirs();
    file("src/main/jniLibs/armeabi-v7a/").mkdirs();
    file("src/main/jniLibs/x86/").mkdirs();

    configurations.natives.files.each { jar ->
        def outputDir = null
        if(jar.name.endsWith("natives-armeabi-v7a.jar")) outputDir = file("src/main/jniLibs/armeabi-v7a")
        if(jar.name.endsWith("natives-armeabi.jar")) outputDir = file("src/main/jniLibs/armeabi")
        if(jar.name.endsWith("natives-x86.jar")) outputDir = file("src/main/jniLibs/x86")
        if(outputDir != null) {
            copy {
                from zipTree(jar)
                into outputDir
                include "*.so"
            }
        }
    }
}
repositories {
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'